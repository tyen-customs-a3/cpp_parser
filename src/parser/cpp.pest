WHITESPACE = _{ " " | "\t" | NEWLINE }
COMMENT = _{ line_comment | block_comment }
line_comment = _{ "//" ~ (!NEWLINE ~ ANY)* ~ (NEWLINE | EOI) }
block_comment = _{ "/*" ~ (!"*/" ~ ANY)* ~ "*/" }

file = { SOI ~ statement* ~ EOI }
statement = _{ enum_block | class_def | delete_statement | property | WHITESPACE }

// Enums
enum_block = { "enum" ~ "{" ~ enum_values ~ "}" ~ ";" }
enum_values = _{ enum_value ~ ("," ~ enum_value)* ~ ","? }
enum_value = { (enum_identifier | integer) ~ "=" ~ integer }
integer = @{ "-"? ~ ASCII_DIGIT+ }

// Delete statement
delete_statement = { "delete" ~ identifier ~ ";" }

// Class definitions
class_def = { class_header ~ class_body? ~ ";" }
class_header = { "class" ~ class_name ~ (":" ~ parent_name)? }
class_name = @{ (ASCII_ALPHANUMERIC | "_")+ }
parent_name = @{ (ASCII_ALPHANUMERIC | "_")+ }
class_body = { "{" ~ class_content* ~ "}" }
class_content = _{ property | class_def | delete_statement | WHITESPACE }

// Properties
property = { property_name ~ array_suffix? ~ assignment_op ~ value ~ ";" }
property_name = @{ identifier }
array_suffix = @{ "[" ~ "]" }
assignment_op = { "+=" | "=" }

// Values
value = { array | macro_call | string_literal | number | identifier }
array = { "{" ~ (array_item ~ ("," ~ array_item)*)? ~ ","? ~ "}" }
array_item = _{ macro_call | string_literal | number | identifier | array }
string_literal = @{ "\"" ~ (escaped_quote | (!"\"" ~ ANY))* ~ "\"" }
escaped_quote = { "\"\"" }
number = @{ "-"? ~ ASCII_DIGIT+ ~ ("." ~ ASCII_DIGIT+)? ~ (^"e" ~ ("+" | "-")? ~ ASCII_DIGIT+)? }
identifier = @{ (ASCII_ALPHA | ASCII_DIGIT | "_") ~ (ASCII_ALPHANUMERIC | "_")* }
enum_identifier = @{ (ASCII_ALPHA | "_") ~ (ASCII_ALPHANUMERIC | "_")* }

// Macro support
macro_call = { macro_name ~ "(" ~ macro_arg ~ ")" }
macro_name = @{ "LIST_" ~ ASCII_DIGIT+ }
macro_arg = { string_literal | number | identifier }